buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.10.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.+'
    }
}

apply plugin: 'android'
apply plugin: 'hugo'

repositories {
    mavenCentral()
    maven { url "http://maven.arcao.com/" }
}

dependencies {
    compile 'com.android.support:support-v4:19.+'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'com.squareup.dagger:dagger:1.2.1'
    compile 'com.squareup.okhttp:okhttp:1.5.4'
    compile 'com.squareup.picasso:picasso:2.2.0'
    compile 'com.jakewharton:butterknife:5.0.+'
    compile 'com.jakewharton.timber:timber:2.2.2'
    compile('com.arcao:geocaching-api:1.5.22') {
      exclude group: 'log4j', module: 'log4j' /* Not working in Android, log4j-over-slf4j used instead */
    }
    compile('oauth.signpost:signpost-commonshttp4:1.2.1.2') {
      exclude group: 'org.apache.httpcomponents', module: 'httpclient' /* already in Android */ 
      exclude group: 'commons-codec', module: 'commons-codec' /* already in Android */
    }
    compile('org.slf4j:log4j-over-slf4j:1.6.4') {
      exclude group: 'org.slf4j', module: 'slf4j-api' /* Not working in Android, slf4j-android used instead */
    }
    compile 'org.slf4j:slf4j-android:1.6.1-RC1'
    compile('com.activeandroid:activeandroid:3.1-2') {
      exclude group: 'com.google.android', module: 'support-v4' // Used SDK bundled artifact instead
    }

    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

// Geocaching API key default value
if (!project.hasProperty('geocachingApiKey')) {
    project.setProperty('geocachingApiKey', '')
}
if (!project.hasProperty('geocachingApiSecret')) {
    project.setProperty('geocachingApiSecret', '')
}
if (!project.hasProperty('geocachingApiStaging')) {
    project.setProperty('geocachingApiStaging', true)
}


android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    dexOptions {
      // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
      preDexLibraries = preDexEnabled && !isTravis
    }

    defaultConfig {
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "GEOCACHING_API_KEY", "\"${geocachingApiKey}\""
        buildConfigField "String", "GEOCACHING_API_SECRET", "\"${geocachingApiSecret}\""
        buildConfigField "boolean", "GEOCACHING_API_STAGING", "${geocachingApiStaging}"
    }

    signingConfigs {
        release {
            keyAlias = "watchmytrackables"
        }
    }

    buildTypes {
        debug {
            packageNameSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard.cfg'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        // removed because of conflict in creating package
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        // unused files, removed to minimize APK file
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'org/apache/http/version.properties'
        exclude 'templates/release-notes.vm'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'log4j.xml'
    }
}

if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = android.signingConfigs.debug
}